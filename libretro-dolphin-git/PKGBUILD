# Maintainer: Alexandre Bouvier <contact@amb.tf>
_pkgname=libretro-dolphin
pkgname=$_pkgname-git
pkgver=r33011.6a0b6ee8a4
pkgrel=1
pkgdesc="Nintendo GameCube/Wii core"
arch=('x86_64')
url="https://github.com/libretro/dolphin"
license=('GPL2')
groups=('libretro')
depends=(
	'bluez-libs'
	'cubeb'
	'enet'
	'fmt'
	'hidapi'
	'libevdev'
	'libretro-core-info'
	'libusb'
	'lzo'
	'mbedtls'
	'pugixml'
	'sfml'
	'systemd-libs'
	'xxhash'
)
makedepends=(
	'bzip2'
	'cmake'
	'curl'
	'git'
	'libglvnd'
	'libpng'
	'python'
	'systemd'
	'xorgproto'
	'xz'
	'zlib'
	'zstd'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=(
	"$_pkgname::git+$url.git"
	'unbundle-libs.patch'
)
b2sums=(
	'SKIP'
	'73ad834e3d166b01407810b28be3b76012133de56587fc6cae6d7f596aa3e6386f88c6d612df11ffe50edb1eeeab0b719a2c98d0a376f22769b7310b0007b012'
)

pkgver() {
	cd $_pkgname
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	patch -d $_pkgname -Np1 < unbundle-libs.patch
}

build() {
	cmake -S $_pkgname -B build \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_LTO=OFF \
		-DENABLE_TESTS=OFF \
		-DLIBRETRO=ON \
		-DUSE_SHARED_ENET=ON \
		-Wno-dev
	cmake --build build
}

package() {
	# shellcheck disable=SC2154
	install -Dm644 -t "$pkgdir"/usr/lib/libretro build/dolphin_libretro.so
}
