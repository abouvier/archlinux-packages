# Maintainer: Alexandre Bouvier <contact@amb.tf>
pkgname=foobar2000
pkgver=1.6.12
pkgrel=1
pkgdesc="An advanced freeware audio player for the Windows platform"
arch=('any')
url="https://www.foobar2000.org"
license=('custom')
depends=('unionfs-fuse' 'wine')
makedepends=('icoutils' 'p7zip')
optdepends=(
	"$pkgname-component-playcount: playback statistics support"
	"$pkgname-component-texttools: custom copy to clipboard support"
	"$pkgname-encoders: encoders for the converter component"
)
source=(
	"$url/files/foobar2000_v$pkgver.exe"
	"$pkgname.bash"
	"$pkgname.desktop"
	"$pkgname.xml"
)
b2sums=(
	'194d87632d0a7960eba3ddc021ff77938000a2a354c9f59f29bc4f74146d7b3bd7a9a701bda398b6914628ea5112e206f50bc4b25cd1ba505b40d8d447669f1f'
	'0e48bc06bfe8d32dba833e48afc69d2126ce2725e1be7a1988f7f14cb204e123745389e748626cc214e3fe9817e21a0b17213660689ab8a5d66df0e9b7ea2d4f'
	'439161d4ffbc16d40b0f65b277166f4c72d07652572858cb9e816819fd3bed90063b5849f253270383dd749de26b4e51fd225521ef4f09e4eb41574643e26908'
	'd7b423930a3c30b837c9450b893b8f112b8051e957c282d8e9b078329a0e8b37c93f78ea136aebace14a1f0851d56f47f790610814db2e6ccdea7980346a9957'
)

prepare() {
	7z x -y foobar2000_v$pkgver.exe
	icotool -x icons/*.ico 2> /dev/null
	wrestool -xRo. foobar2000.exe
}

package() {
	# shellcheck disable=SC2154
	install -Dm755 $pkgname.bash "$pkgdir"/usr/bin/$pkgname
	install -Dm755 -t "$pkgdir"/usr/share/$pkgname foobar2000.exe
	install -Dm644 -t "$pkgdir"/usr/share/$pkgname -- *.dll
	install -Dm644 -t "$pkgdir"/usr/share/$pkgname/doc doc/*.{html,css}
	install -Dm644 -t "$pkgdir"/usr/share/$pkgname/themes themes/*.fth
	install -Dm644 -t "$pkgdir"/usr/share/$pkgname/components components/*.dll
	install -Dm644 -t "$pkgdir"/usr/share/$pkgname/runtime runtime/*.{dll,manifest}
	for type in fpl=playlist fth=theme dll=component; do
		for size in 16 32 48 256; do
			install -Dm644 ${type%=*}_*_${size}x${size}x*.png \
				"$pkgdir"/usr/share/icons/hicolor/${size}x${size}/mimetypes/application-x-fb2k-${type#*=}.png
		done
	done
	install -Dm644 foobar2000.exe_PNG_269 "$pkgdir"/usr/share/icons/hicolor/256x256/apps/$pkgname.png
	install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname doc/license.html
	install -Dm644 -t "$pkgdir"/usr/share/applications $pkgname.desktop
	install -Dm644 -t "$pkgdir"/usr/share/mime/packages $pkgname.xml
	touch "$pkgdir"/usr/share/$pkgname/portable_mode_enabled
}
